buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'Modmuss50'
            url = 'http://maven.modmuss50.me/'
        }
    }
    dependencies {
		classpath 'chorusmc:ChorusGradle:0.0.1.+'
    }
}

plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'
}
apply plugin: 'chorus.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'chorusmc'
archivesBaseName = project.name.toLowerCase()
version = minecraft_version + '-0.0.1'

// Fetch build number from Jenkins
def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'SpongePowered'
        url = 'http://repo.spongepowered.org/maven'
    }
}

minecraft {
    version = minecraft_version
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile ('net.minecraft:launchwrapper:1.11') {
        transitive = false
    }
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'com.typesafe:config:1.3.0'
    compile('org.spongepowered:mixin:0.5.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }
	compile 'com.mojang:authlib:1.5.22'
	compile files(".gradle/minecraft/${minecraft_version}-client-mapped.jar")
}

jar {
    from '.gradle/minecraft/mappings/'

    manifest {
        attributes(
                'MinecraftVersion': minecraft_version
        )
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('mavenPass')) {
                repository(url: 'http://mavenupload.modmuss50.me/') {
                    authentication(userName: 'buildslave', password: project.getProperty('mavenPass'))
                }
            } else {
               repository(url: 'file:///var/www/maven/')
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = 'https://github.com/ChorusMC/chorus-base'
                        connection = 'scm:git:git@github.com:ChorusMC/chorus-base.git'
                        developerConnection = 'scm:git:git@github.com:ChorusMC/chorus-base.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/ChorusMC/chorus-base/issues'
                    }

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/ChorusMC/chorus-base/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'modmuss50'
                            name = 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
